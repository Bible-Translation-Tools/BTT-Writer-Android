apply plugin: 'com.android.application'

buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.1'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.6.1'
    }
}

apply plugin: 'com.getkeepsafe.dexcount'

android {
    signingConfigs {
        if (System.getenv("TRAVIS")) {
            release {
                storeFile rootProject.file('translationstudio.keystore')
                storePassword System.getenv("KEYSTORE_PASS")
                keyAlias System.getenv("ALIAS_NAME")
                keyPassword System.getenv("ALIAS_PASS")
                v2SigningEnabled true
            }
        }
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "org.bibletranslationtools.writer.android"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 3
        versionName "1.0.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    } 
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFile('proguard-rules.pro')
            if (System.getenv("TRAVIS")) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFile('proguard-rules.pro')
        }
        applicationVariants.all { variant ->
            if(variant.buildType.name == "release") {
                variant.outputs.all {
                    outputFileName = "release.apk"
                }
            }
        }
    }
    lintOptions {
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
    }
}

configurations {
    all {
        exclude module: 'httpclient'
        exclude module: 'commons-logging'
    }
}

if (System.env.TRAVIS == 'true') {
    allprojects {
        tasks.withType(GroovyCompile) {
            groovyOptions.fork = false
        }
        tasks.withType(Test) {
            // containers (currently) have 2 dedicated cores and 4GB of memory
            maxParallelForks = 2
            minHeapSize = '128m'
            testLogging {
                // Custom configuration
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'org.eclipse.jgit:org.eclipse.jgit:3.7.1.+' // new version available 4.6.0
    implementation 'com.jcraft:jsch:0.1.53'
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    implementation 'com.jpardogo.materialtabstrip:library:1.0.8'
    implementation 'com.github.filippudak.progresspieview:library:1.0.4'
    implementation 'org.apmem.tools:layouts:1.10'
    implementation 'com.itextpdf:itextpdf:5.5.10'
    implementation 'com.facebook.rebound:rebound:0.3.8'
    implementation 'org.unfoldingword.tools:gogs-client:1.6.1'
    implementation 'org.unfoldingword.tools:task-manager:1.5.3'
    implementation 'org.unfoldingword.tools:door43-client:0.10.3'
    implementation 'org.unfoldingword.tools:http:2.4.2'
    implementation 'org.unfoldingword.tools:logger:2.0.0'
    implementation 'org.unfoldingword.tools:event-buffer:1.0.2'
    implementation 'org.unfoldingword.tools:foreground:0.1.0'
    implementation project(':html-textview')
    implementation project(':seekbarhint')
    implementation 'com.google.android.gms:play-services-appindexing:9.8.0'
    androidTestImplementation 'androidx.recyclerview:recyclerview:1.0.0'
    androidTestImplementation 'androidx.appcompat:appcompat:1.0.0'
    androidTestImplementation 'androidx.legacy:legacy-support-v4:1.0.0'
    androidTestImplementation 'com.google.android.material:material:1.0.0'
    androidTestImplementation 'androidx.annotation:annotation:1.0.0'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test:rules:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    androidTestImplementation('androidx.test.espresso:espresso-contrib:3.1.0') {
        // Necessary to avoid version conflicts
//        exclude group: 'com.android.support', module: 'appcompat'
//        exclude group: 'com.android.support', module: 'support-v4'
//        exclude group: 'com.android.support', module: 'support-annotations'
//        exclude module: 'recyclerview-v7'
    }
    //    // Optional -- UI testing with UI Automator

    //    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.1'
    testImplementation 'junit:junit:4.12'
    implementation 'org.markdownj:markdownj-core:0.4'
}
